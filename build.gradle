buildscript {
    apply from: "$rootDir/dependencies.gradle"

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath classpaths.gradleAndroidPlugin
        classpath classpaths.gradleKotlinPlugin
    }
}

apply from: "$rootDir/application.gradle"

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    if (project.name == "app") {
        apply plugin: "com.android.application"
    } else {
        apply plugin: "com.android.library"
    }

    apply plugin: "kotlin-android"

    android {
        compileSdkVersion application.targetedSdkVersion
        buildToolsVersion application.buildToolsVersion

        defaultConfig {
            minSdkVersion application.minSdkVersion
            targetSdkVersion application.targetedSdkVersion
            versionCode application.versionCode
            versionName application.versionName

            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        kotlinOptions {
            jvmTarget = '1.8'
        }

        buildFeatures {
            viewBinding true
        }
    }

    dependencies {
        implementation(libraries.coroutinesAndroid)
        implementation(libraries.timber)
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}